ext {
    querydslGeneratedSourcesDir = file("$buildDir/generated/sources/querydsl")
}
dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'com.querydsl:querydsl-jpa'

    annotationProcessor 'com.querydsl:querydsl-apt'

    runtimeOnly "org.liquibase:liquibase-core:$VER_LIQUIBASE"
    runtimeOnly 'mysql:mysql-connector-java'
}

sourceSets {
    main {
        java {
            srcDirs += querydslGeneratedSourcesDir
        }
    }
}


task generateQuerydsl(type: JavaCompile, group: 'build',
    description: "Generates the QueryDSL query types") {
    doFirst {
        delete querydslGeneratedSourcesDir
        querydslGeneratedSourcesDir.mkdirs()
    }
    source = sourceSets.main.java
    classpath = sourceSets.main.compileClasspath
    options.annotationProcessorPath = sourceSets.main.compileClasspath + configurations.annotationProcessor
    destinationDir = querydslGeneratedSourcesDir

    // Querydsl compile
    options.compilerArgs = ["-proc:only",
                            "-processor", "com.querydsl.apt.jpa.JPAAnnotationProcessor",
                            "-s", querydslGeneratedSourcesDir] as List<String>
}

compileJava.dependsOn generateQuerydsl
